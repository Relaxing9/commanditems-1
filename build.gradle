plugins {
    id 'com.gradleup.shadow' version "8.3.4"
    id 'java'
}

group 'me.relaxing9.commanditems'
version '2.6.7'

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
    mavenLocal()

    maven { url "https://hub.spigotmc.org/nexus/content/groups/public/" }
    maven { url "https://repo.codemc.org/repository/maven-public/" }
    maven { url "https://repo.aikar.co/content/groups/aikar/" }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.15.2-R0.1-SNAPSHOT"

    implementation "org.bstats:bstats-bukkit:3.1.0"

    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"

    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.17.2"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.18.1"
    implementation 'com.jeff-media:MorePersistentDataTypes:2.4.0'

}

shadowJar {
    archiveBaseName = 'CommandItems'

    relocate 'org.bstats', "${group}.lib.bstats"

    relocate 'co.aikar.commands', "${group}.lib.acf"
    relocate 'org.intellij', "${group}.lib.intellij"
    relocate 'org.jetbrains', "${group}.lib.jetbrains"
    relocate 'net.jodah', "${group}.lib.jodah"

    relocate 'org.yaml.snakeyaml', "${group}.lib.snakeyaml"
    relocate 'com.fasterxml', "${group}.lib.fasterxml"

    relocate 'com.jeff_media.morepersistentdatatypes', "${group}.lib.morepersistentdatatypes"
}

task install(type: Copy) {
    from("$rootDir/build/libs/") {
        include "**/CommandItems-*.jar"
    }
    rename("([^-]*).*\\.jar", '$1.jar')
    into "$rootDir/testenv/plugins/"
}
